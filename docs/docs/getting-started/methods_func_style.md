---
sidebar_position: 3
tags: [Начало]
---

# Описание HTTP методов

Пример описания метода в функциональном стиле:

```bsl
МассивОписанийМетодов = Swag_Описание
	.Метод("ТестовыйGET")
		.ОписаниеМетода("Получение данных из 1С")
		.ПараметрURL("Версия")
			.ОписаниеПараметра("Версия метода")
			.ЗначениеПеречисления("1.0")
			.ЗначениеПеречисления("2.0")
		.Ответ(200)
			.ОписаниеОтвета("Success")
	.Сформировать()
;
```

## Наименование метода

Наименование метода формируется по принципу:

![Формирование наименования метода](../../static/img/method-name.png)

## Текстовое описание метода

### Основное описание (summary)

```bsl
МассивОписанийМетодов = Swag_Описание
	.Метод("ТестовыйGET")
		.ОписаниеМетода("Получение данных из 1С")
	.Сформировать()
;
```

### Дополнительное описание (description)

:::info

Данный функционал добавлен в версии 1.2.4.

:::

```bsl
МассивОписанийМетодов = Swag_Описание
	.Метод("ТестовыйGET")
		.ДополнительноеОписаниеМетода("В дополнительном описании можно вставлять гиперссылки <a href=""http://example.com"">example.com</a>")
	.Сформировать()
;
```

## Входящие параметры

Параметры метода можно передать четыремя разными способами:

### Параметры URL

для URL ```/{Версия}/test```
```http
GET /1.0/test HTTP/1.1
Host: example.com
```

```bsl
МассивОписанийМетодов = Swag_Описание
	.Метод("ТестовыйGET")
		.ПараметрURL("Версия")
	.Сформировать()
;
```

### Параметры запроса

```http
GET /test?version=1.0 HTTP/1.1
Host: example.com
```

```bsl
МассивОписанийМетодов = Swag_Описание
	.Метод("ТестовыйGET")
		.ПараметрЗапроса("version")
	.Сформировать()
;
```

### Заголовки

```http
GET /test HTTP/1.1
Host: example.com
X-Api-Version: 1.0
```

```bsl
МассивОписанийМетодов = Swag_Описание
	.Метод("ТестовыйGET")
		.ПараметрЗаголовка("X-Api-Version")
	.Сформировать()
;
```

### Форму

```http
POST /test HTTP/1.1
Host: example.com
Content-Type: application/x-www-form-urlencoded; charset=utf-8
Content-Length: 11
version=1.0
```

```bsl
МассивОписанийМетодов = Swag_Описание
	.Метод("ТестовыйPOST")
		.ПараметрФормы("version")
	.Сформировать()
;
```

### Дополнительные описания параметров

#### Обязательный

```bsl
МассивОписанийМетодов = Swag_Описание
	.Метод("ТестовыйGET")
		.ПараметрЗапроса("version")
		// highlight-next-line
		.Обязательный()
	.Сформировать()
;
```

#### Минимальная и максимальная длина

:::info

В swagger-ui информация о длине параметров не отображается. Зато в других интерфейсах (redoc, rapidoc, scalar и stoplight) всё отображается корректно.

:::

```bsl
МассивОписанийМетодов = Swag_Описание
	.Метод("ТестовыйGET")
		.ПараметрЗапроса("version")
		// highlight-start
		.МинимальнаяДлина(10)
		.МаксимальнаяДлина(255)
		// highlight-end
	.Сформировать()
;
```

#### Перечисление

```bsl
МассивОписанийМетодов = Swag_Описание
	.Метод("ТестовыйGET")
		.ПараметрЗапроса("version")
		// highlight-start
		.ЗначениеПеречисления("1.0")
		.ЗначениеПеречисления("2.0")
		// highlight-end
	.Сформировать()
;
```

### Передача в запрос тела запроса

:::info

Стандарт HTTP никак не регламентирует передачу тела запроса в зависимости от метода. Но скорее всего при методе GET web-сервер проигнорирует переданное тело.

:::

```bsl
МассивОписанийМетодов = Swag_Описание
	.Метод("ТестовыйPOST")
		.ОписаниеМетода("Отправка данных в 1С")
		.Тело()
			.ТипТела("application/json")
				.СхемаТела("ТестовыйОбъект")
	.Сформировать()
;
```

### Комбинирование (объединение) типов данных в теле запроса

:::info

Для метода ОбъединениеСхемТела допустимы следующие значения: oneOf, anyOf, allOf, not

:::

```bsl
МассивОписанийМетодов = Swag_Описание
	.Метод("ТестовыйPOST")
		.ОписаниеМетода("Отправка данных в 1С")
		.Тело()
			.ТипТела("application/json")
				.ОбъединениеСхемТела("oneOf")
					.СхемаТела("ТестовыйОбъект")
					.СхемаТела("ЕщёОдинОбъект")
					.СхемаТела("ТретийОбъект")
	.Сформировать()
;
```

## Исходящие данные

Данные возвращаемые HTTP-сервисом формируются из комбинации кода ответа, MIME-типа и описания возвращаемого объекта или массива.

```bsl
МассивОписанийМетодов = Swag_Описание
	.Метод("ТестовыйGET")
		// Код ответа
		.Ответ(200)
			.ОписаниеОтвета("Success")
			// MIME-тип ответа
			.ТипОтвета("application/json")
				// Схема объекта в ответе
				.СхемаОтвета("ТестовыйОбъект")
	.Сформировать()
;
```

### Комбинирование (объединение) типов данных в ответе запроса

```bsl
МассивОписанийМетодов = Swag_Описание
	.Метод("ТестовыйGET")
		// Код ответа
		.Ответ(200)
			.ОписаниеОтвета("Success")
			// MIME-тип ответа
			.ТипОтвета("application/json")
				// Способ объединения схем
				.ОбъединениеСхемТела("oneOf")
					// Схема объекта в ответе
					.СхемаОтвета("ТестовыйОбъект")
					.СхемаОтвета("ЕщёОдинОбъект")
					.СхемаОтвета("ТретийОбъект")
	.Сформировать()
;
```
